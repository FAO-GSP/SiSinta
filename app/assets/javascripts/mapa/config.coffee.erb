# Cuadro de información con título y texto
L.Control.Info = L.Control.extend({
  options:
    position: 'topleft'

  onAdd: (map) ->
    wrapper = L.DomUtil.create('div', 'info')
    title = L.DomUtil.create('h6', 'col title', wrapper)
    text = L.DomUtil.create('p', 'col text', wrapper)

    title.innerHTML = this.options.title
    text.innerHTML = this.options.text

    return wrapper

  onRemove: (map) ->
    # Nada que hacer
})

# Factory function named after the class of the control plugin to allow chaining
L.control.info = (opts) ->
  return new L.Control.Info(opts)

# exports
# https://robots.thoughtbot.com/module-pattern-in-javascript-and-coffeescript
window.Mapa = {}

# Binds a popup box for each feature
Mapa.prepare_popup = (feature, layer) ->
  layer.bindPopup Mapa.popup_markup(feature.properties)

# Generates the markup for each popup
Mapa.popup_markup = (feature) ->
  serie = feature.serie

  serie_html =
    if serie
      "<a target='_blank' title='Serie' href='#{serie.url}'>#{serie.nombre || serie.id}</a> - "
    else
      ''

  [
    serie_html,
    "<a target='_blank' title='feature' href='#{feature.url}'>",
    "#{feature.numero || feature.id}",
    '</a>'
  ].join('')

# Generates a colored circle marker for each feature
Mapa.prepare_marker = (point, latlng) ->
  # https://clrs.cc
  olive = '#3D9970'
  orange = '#FF851B'

  style =
    radius: 5
    weight: 2
    fillOpacity: 0.5
    color: if point.properties.publico then olive else orange

  return L.circleMarker(latlng, style)
